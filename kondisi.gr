$m.sidang("Ekspresi Kondisi", fn (uji) {
    uji("saat benar dan hanya memiliki blok kebenaran", fn() {
        ada nilai = 0
        tuntut nilai == 0

        jika (benar) {
            nilai = 1
        }

        tuntut nilai == 1
    })

    uji("saat salah dan hanya memiliki blok kebenaran", fn() {
        ada nilai = 0

        jika (salah) {
            nilai = 1
        }

        tuntut nilai == 0
    })

    uji("saat nil dan hanya memiliki blok kebenaran", fn() {
        ada nilai = 0

        jika (nil) {
            nilai = 1
        }

        tuntut nilai == 0
    })

    uji("saat bernilai sebenar dan hanya memiliki blok kebenaran", fn() {
        ada nilai = 0

        jika (1) {
            nilai += 2
        }

        jika ("gara") {
            nilai -= 1
        }

        tuntut nilai == 1
    })

    uji("bisa mengutus dari blok paling dalam", fn () {
        ada a

        a = fn () {
            jika (10 > 1) {
                jika (10 > 1) {
                    utus 1
                }

                utus 2
            }
        }()

        tuntut a == 1
    })

    uji("saat salah dan memiliki blok lain", fn () {
        ada a

        jika (salah) {
            a = 1
        } lain {
            a = 2
        }

        tuntut a == 2
    })

    tentang("dengan berbagai cabang alternatif berkondisi", fn () {
        xuji("tidak boleh menggunakan cabang alternatif berkondisi setelah cabang alternatif tak berkondisi", fn () {
            // setelah memiliki fungsi eval() & multi-line string
            // coba {
            //     jika (benar) { }
            //     lain { }
            //     lain jika (salah) { }
            // } tahan (e) {

            // }
        })

        tentang("cabang utama bernilai benar", fn () {
            uji("hanya mengeksekusi cabang utama meski terdapat 1 cabang alternatif berkondisi", fn () {
                ada a
                tuntut a == nil

                jika (benar) {
                    a = 1
                } lain jika (benar) {
                    a = 2
                }

                tuntut a == 1

                a = nil
                tuntut a == nil

                jika (benar) {
                    a = 1
                } lain jika (benar) {
                    a = 2
                } lain {
                    a = 3
                }

                tuntut a == 1
            })

            uji("hanya mengeksekusi cabang utama meski terdapat 2 cabang alternatif berkondisi", fn () {
                ada a
                tuntut a == nil

                jika (benar) { a = 1 }
                lain jika (benar) { a = 2 }
                lain jika (benar) { a = 3 }

                tuntut a == 1

                a = nil
                tuntut a == nil

                jika (benar) { a = 1 }
                lain jika (benar) { a = 2 }
                lain jika (benar) { a = 3 }
                lain { a = 4 }

                tuntut a == 1
            })
        })

        tentang("cabang utama bernilai salah", fn () {
            uji("mengeksekusi cabang alternatif berkondisi pertama yang bernilai benar", fn () {
                ada a

                jika (salah) { a = 1 }
                lain jika (benar) { a = 2 }
                lain jika (benar) { a = 3 }
                lain { a = 4 }

                tuntut a == 2

                a = nil
                tuntut a == nil

                jika (salah) { a = 1 }
                lain jika (salah) { a = 2 }
                lain jika (benar) { a = 3 }
                lain { a = 4 }

                tuntut a == 3

                a = nil
                tuntut a == nil

                jika (salah) { a = 1 }
                lain jika (salah) { a = 2 }
                lain jika !(salah) { a = 3 }
                lain { a = 4 }

                tuntut a == 3
            })

            uji("mengeksekusi cabang alternatif tak berkondisi jika ada dengan catatan seluruh cabang yang berkondisi bernilai salah", fn () {
                ada a

                jika (salah) { a = 1 }
                lain jika (salah) { a = 2 }
                lain jika (salah) { a = 3 }
                lain jika !(benar) { a = 4 }
                lain { a = 5 }

                tuntut a == 5
            })

            uji("tidak mengeksekusi blok manapung jika tidak ada cabang yang bernilai sebenar", fn () {
                ada a

                jika (salah) { a = 1 }
                lain jika (salah) { a = 2 }
                lain jika (salah) { a = 3 }
                lain jika (salah) { a = 4 }
                lain jika !(benar) { a = 5 }

                tuntut a == nil
            })
        })
    })
})
