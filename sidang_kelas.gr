sidang(Kelas, fn () {
    ia("itu Kelas", fn () {
        kelas Siswa {}

        tuntut Siswa itu Kelas
        tuntut Siswa itu Objek
        tuntut 2 itu Siswa == salah
        tuntut Siswa.baru() itu Siswa
    })

    tentang("nama", fn () {
        ia("harus diawali huruf kapital", fn () {
            tuntut coba {
                kelas a {}
                salah
            } tahan (e) {
                tuntut e.pesan() == "Nama kelas harus diawali huruf kapital"
            } == benar
        })

        ia("diutus melalui metode .nama()", fn () {
            tuntut Masalah.nama() == "Masalah"

            kelas Siswa {}
            tuntut Siswa.nama() == "Siswa"

            ada s = Siswa.baru()
            tuntut s.metode().berisi?("nama") == salah

            tuntut 12.metode().berisi?("nama") == salah
            tuntut 12.5.metode().berisi?("nama") == salah
        })
    })

    tentang("#baru", fn () {
        ia("bisa menginisialisasi kelas", fn () {
            kelas Siswa {}

            ada siswa = Siswa.baru()

            tuntut siswa != nil
            tuntut siswa itu Siswa
            tuntut siswa itu Objek
        })
    })

    ia(".tipe() bisa lebih spesifik dari `itu`", fn () {
        kelas Siswa {}

        tuntut Siswa.baru().tipe() == Siswa
        tuntut 1 itu Angka
        tuntut 1 itu Integer
        tuntut 1.tipe() == Integer
        tuntut 1.tipe() != Angka
    })

    tentang("arijawab", fn () {
        ia("tidak merembet keluar", fn () {
            ada penghitungTanggalLahir

            kelas Tanggalanku {
                ada penghitungTanggalLahir = fn (umur) {
                    2020 - umur
                }

                fn tanggalLahir() {
                    tuntut penghitungTanggalLahir != nil
                    penghitungTanggalLahir(29)
                }

                fn definisiDalam() {}
            }

            tuntut Tanggalanku.baru().tanggalLahir() == 1991
            tuntut penghitungTanggalLahir == nil

            ada tidakTerdefinisikan = salah
            coba { definisiDalam() } tahan (e) { tidakTerdefinisikan = benar }
            tuntut tidakTerdefinisikan == benar
        })

        tentang("pemanggilan tanpa tanda kurung", fn () {
            tentang("pada metode umum", fn () {
                ia("bisa dilakukan jika metode tak memiliki argumen", fn () {
                    tidakAda!(fn () { A })
                    kelas A { fn a() { "a" } }
                    tuntut A.baru().a.besar == "A"
                    tuntut A.baru().a().besar() == "A"
                })

                ia("tidak bisa dilakukan jika metode tak memiliki argumen", fn () {
                    tidakAda!(fn () { A })
                    kelas A { fn a(arg) { arg } }

                    tuntut coba {
                        A.baru().a.besar == "A"
                        salah
                    } tahan (e) {
                        tuntut e.pesan() == "tidak dapat meresolusi `a` dalam objek bertipe A"
                        benar
                    } == benar
                })
            })

            tentang("pada metode pribadi", fn () {
                ia("bisa dilakukan di dalam kelas", fn () {
                    tidakAda!(fn () { A })

                    kelas A {
                        fn b() { a.besar }

                        pribadi {
                            fn a() { "a" }
                        }
                    }

                    ada a = A.baru()
                    tuntut a.b == "A"
                    tuntut a.b() == "A"
                })
            })

            tentang("pada fungsi statis umum", fn () {
                ia("bisa dilakukan", fn () {
                    tidakAda!(fn () { A })

                    kelas A {
                        fn sta a() { b }
                        fn sta b() { "b" }
                        fn sta c() { a.besar }
                    }

                    tuntut A.c == "B"
                    tuntut A.c() == "B"
                })
            })

            tentang("pada fungsi statis pribadi", fn () {
                ia("hanya bisa dilakukan dari dalam kelas", fn () {
                    tidakAda!(fn () { A })

                    kelas A {
                        fn sta ab() { a.besar + b.besar }
                        pribadi {
                            fn sta a() { "a" }
                            fn sta b() { "b" }
                        }
                    }

                    tuntut A.ab() == "AB"
                    tuntut A.ab == "AB"
                })
            })
        })

        ia("tidak bisa diresolusi seperti pengenal variabel", fn () {
            tidakAda!(fn () { A })

            kelas A {
                ada a = "a"
                fn a () { a }

                fn b() { a }
            }

            ada a = A.baru()
            tuntut a.a() == "a"
            tuntut a.b() == "a"
        })

        ia("bisa berakhiran `=`", fn () {
            kelas A {
                ada a

                fn a() { a }
                fn a=(x) {
                    a = x
                }
            }

            ada a = A.baru()
            tuntut a.a() == nil
            tuntut a.a=(12)
            tuntut a.a() == 12
            tuntut a.a = 13
            tuntut a.a() == 13
        })

        ia("bisa bernama mirip dengan variabel tapi tidak boleh menutup akses ke variabel tersebut", fn () {
            kelas Data {
                ada nilai = 1
                fn nilai() { nilai }
            }

            tuntut Data.baru().nilai() == 1
        })

        ia("dapat berpolimorfis", fn () {
            kelas Penyapa {
                fn sapa(nama) {
                    "Hai, " + nama
                }

                fn sapa(sapaan, nama) {
                    sapaan + nama
                }
            }

            ada penyapa = Penyapa.baru()
            tuntut penyapa.sapa("Gara") == "Hai, Gara"
            tuntut penyapa.sapa("Yo! ", "Gara") == "Yo! Gara"
        })

        ia("tidak dianggap sebagai fungsi statis", fn () {
            tidakAda!(fn () { A })

            kelas A {
                fn a () { "a" }
            }

            tuntut coba {
                A.a()
                salah
            } tahan (e) {
                tuntut e.pesan() == "kelas A tidak memiliki fungsi statis `a` tak berargumen yang bisa dipanggil"
                benar
            } == benar
        })

        ia("bisa bernama sama dengan metode kelas bawaan", fn () {
            ada a = 1
            a.tambah(1) == 2

            kelas Integer {
                fn tambah(a, b) { objekIni() + a + b }
            }

            a.tambah(1, 1) == 3
        })
    })

    ia("bisa diperkaya meski telah didefinisikan", fn () {
        kelas Siswa {
            fn namaKapital() { "GARA" }
        }

        ada siswa = Siswa.baru()
        ada namaKapitalLama = siswa.namaKapital()

        tuntut siswa.sambut?("fungsiBaruYangBelumAda") == salah

        kelas Siswa {
            fn fungsiBaruYangBelumAda() {
                benar
            }
        }

        kelas Siswa {
            fn namaKapital() {
                "Gara"
            }
        }

        tuntut siswa.namaKapital() != namaKapitalLama
        tuntut siswa.fungsiBaruYangBelumAda() == benar
    })

    ia("pemerkayaan bersifat global dan permanen", fn () {
        kelas X1 {
        }

        ada x1Lama = X1.baru()

        fn () {
            kelas X1 {
                fn x1() { 1 }
            }
        }()

        tuntut X1.baru().x1() == 1
        tuntut x1Lama.x1() == 1
    })

    ia("dapat memperkaya kelas bawaan", fn () {
        kelas Deret {
            ada dataBaru

            fn dataBaru(d) { dataBaru = d }
            fn dataBaru() { dataBaru }
        }

        kelas Boolean {
            fn bolehAtauTidak() {
                "boleh"
            }
        }

        ada d = [1, 2, 3]
        d.dataBaru(10)
        tuntut d.dataBaru() == 10
        tuntut [].dataBaru() == nil

        ada b = benar
        tuntut b.bolehAtauTidak() == "boleh"
        tuntut salah.bolehAtauTidak() == "boleh"
    })

    tentang("variabel instansi", fn () {
        tentang("pemberian nilai", fn () {
            tentang("saat bersifat umum", fn () {
                tentang("tanpa metode penyetel", fn () {
                    ia("bisa dilakukan jika variabel instansi bersifat umum", fn () {
                        tidakAda!(fn () { A })

                        kelas A {
                            ada a
                            fn a() { a }

                            fn sta baru(x) {
                                ada obj = baru()
                                obj.a = x
                                obj
                            }
                        }

                        ada a = A.baru(12)
                        tuntut a.a() == 12

                        a.a = 13
                        tuntut a.a() == 13
                    })
                })

                tentang("dengan metode penyetel", fn () {
                    ia("bisa dilakukan dimanapun", fn () {
                        tidakAda!(fn () { A })

                        kelas A {
                            ada a
                            fn a() { a }
                            fn a=(x) { a = x + 1 } // + 1 memastikan bahwa yang digunakan adalah penyetel

                            fn sta baru(x) {
                                ada obj = baru()
                                obj.a = x
                                obj
                            }
                        }

                        ada a = A.baru(12)
                        tuntut a.a() == 13

                        a.a = 13
                        tuntut a.a() == 14
                    })
                })
            })

            tentang("saat bersifat pribadi", fn () {
                tentang("tanpa metode penyetel", fn () {
                    ia("hanya bisa dilakukan dalam kelas yang sama", fn () {
                        tidakAda!(fn () { A })

                        kelas A {
                            fn sta baru(x) {
                                ada obj = baru()
                                obj.a = x
                                obj
                            }

                            fn a () { a }

                            pribadi {
                                ada a
                            }
                        }

                        tuntut A.baru(123).a() == 123
                        tuntut A.baru(456).a() == 456
                    })
                })

                tentang("dengan metode penyetel", fn () {
                    ia("bisa dilakukan dimanapun", fn () {
                        tidakAda!(fn () { A })

                        kelas A {
                            fn a=(n) { ini.a = n }
                            fn a() { a }

                            pribadi {
                                ada a
                            }
                        }

                        ada a = A.baru()
                        tuntut a.a() == nil
                        a.a = 12
                        tuntut a.a() == 12
                    })
                })
            })
        })

        tentang("pengambilan nilai", fn () {
            tentang("saat bersifat umum", fn () {
                tentang("tanpa metode pembaca", fn () {
                    ia("tetap bisa dibaca", fn () {
                        tidakAda!(fn () { A })

                        kelas A {
                            ada a = 123
                        }

                        tuntut A.baru().a() == 123
                    })

                    ia("bisa dieksekusi hampir mirip metode jika berisi fungsi", fn () {
                        tidakAda!(fn () { A })

                        kelas A {
                            ada ivarLain = 123

                            ada a1 = fn aa1 () { "a" }

                            // gagal, karena bukan metode
                            ada a2 = fn aa2 () {
                                ini.iVarLain = "XYZ"
                            }

                            pribadi {
                                ada b1 = fn b2 () { "b" }
                            }
                        }

                        ada a = A.baru()
                        tuntut a.a1()() == "a"

                        tuntut coba {
                            a.a2()()
                            salah
                        } tahan (e) {
                            tuntut e.pesan() == "identitas belum terdefinisikan: iVarLain"
                            benar
                        } == benar
                    })
                })
            })

            tentang("saat bersifat pribadi", fn () {
                ia("hanya bisa dibaca dari dalam", fn () {
                    tidakAda!(fn () { A })

                    kelas A {
                        fn a1() { a }

                        pribadi {
                            ada a = 123
                        }
                    }

                    ada a = A.baru()
                    tuntut a.a1() == 123

                    tuntut coba {
                        a.a
                        salah
                    } tahan (e) {
                        tuntut e.pesan() == "tidak dapat meresolusi `a` dalam objek bertipe A"
                        benar
                    } == benar
                })
            })
        })

        ia("dapat memiliki variabel instansi yang bernilai beda tiap objek", fn () {
            kelas Penghitung {
                ada angka

                // pengambil dan pengisi
                fn angka() { angka }
                fn angka(angka) { ini.angka = angka }

                fn tambah(angka) { ini.angka += angka }
                fn kurang(angka) { ini.angka -= angka }
            }

            ada p1 = Penghitung.baru()
            ada p2 = Penghitung.baru()
            ada p3 = Penghitung.baru()

            p1.angka(0)
            p2.angka(1)
            tuntut p1.angka() == 0
            tuntut p2.angka() == 1
            tuntut p3.angka() == nil

            p1.tambah(1)
            p1.tambah(1)
            p2.tambah(5)
            p1.tambah(1)
            tuntut p1.angka() == 3
            tuntut p2.angka() == 6

            p1.kurang(1)
            tuntut p1.angka() == 2
            p1.kurang(3)
            tuntut p1.angka() == -1

            tuntut p1.angka() != 0
            p1.angka(0)
            tuntut p1.angka() == 0
        })

        ia("dapat memiliki variabel instansi dengan nilai awal sesuai definisi", fn () {
            kelas Penghitung {
                ada angka1 = 5
                ada angka2 = 10

                fn angka1(angka1) { ini.angka1 = angka1 }
                fn angka2(angka2) { ini.angka2 = angka2 }

                fn jumlah() { angka1 + ini.angka2 }
                fn kurang() { ini.angka1 - angka2 }
                fn bagi() { ini.angka1 / ini.angka2 }
            }

            ada p1 = Penghitung.baru()
            ada p2 = Penghitung.baru()

            p2.angka1(30);

            tuntut p1.jumlah() == 15
            tuntut p2.jumlah() == 40

            tuntut p1.kurang() == -5
            tuntut p2.kurang() == 20

            tuntut p1.bagi() == 0
            tuntut p2.bagi() == 3
        })

        ia("`ini` dapat menyasar variabel intansi tanpa ambiguitas", fn () {
            kelas A {
                ada a

                fn a(n) {
                    ada a = 5 + n
                    a += 5
                    ini.a = a + 5
                    a
                }

                fn a() { a }
            }

            ada a = A.baru()

            tuntut a.a(1) == 11
            tuntut a.a() == 5 + 1 + 5 + 5 // a tetap objek a bukan integer

            tuntut a.a(2) == 12
            tuntut a.a() == 5 + 2 + 5 + 5
        })

        ia("dapat merefer ke variabel instansi atau metode tanpa `ini` selama tidak ambigu", fn () {
            kelas Penilai {
                ada nilai = 10

                fn nilai() { nilai }
                fn jumlahDengan(n) { nilai += n }
                fn jumlahLebihBanyakDengan(n) {
                    ada jumDg = jumlahDengan(n) + 100
                    nilai += jumDg
                }
            }

            ada p = Penilai.baru()
            ada p2 = Penilai.baru()
            tuntut p.nilai() == 10
            tuntut p2.nilai() == 10

            p.jumlahDengan(101)
            tuntut p.nilai() == 111

            p.jumlahLebihBanyakDengan(104)
            tuntut p.nilai() == 530 // 111 + 104 + (215 + 100)
        })

        ia("variabel instansi yang bernilai fungsi tidak boleh dianggap sebagai metode", fn () {
            kelas A {
                ada a1 = fn a2 () { "a" }

                fn bisa() { a1() + b1() }
                fn tidakBisa() { a2() + b2() }

                pribadi {
                    ada b1 = fn b2 () { "b" }
                }
            }

            ada a = A.baru()

            tuntut coba {
                a.a2()
                salah
            } tahan (e) {
                tuntut e.pesan() == "objek tipe A tidak memiliki metode `a2` tak berargumen yang bisa dipanggil"
                benar
            } == benar

            tuntut !a.metode().berisi?("a2")

            tuntut coba {
                a.b1()
                salah
            } tahan (e) {
                tuntut e.pesan() == "objek tipe A tidak memiliki metode `b1` tak berargumen yang bisa dipanggil"
                benar
            } == benar

            tuntut coba {
                a.b2()
                salah
            } tahan (e) {
                tuntut e.pesan() == "objek tipe A tidak memiliki metode `b2` tak berargumen yang bisa dipanggil"
                benar
            } == benar

            tuntut a.bisa() == "ab"

            tuntut coba {
                a.tidakBisa()
                salah
            } tahan (e) {
                tuntut e.pesan() == "identitas tidak ditemukan: a2"
                benar
            }
        })

        tentang("ketika bernilai Fungsi", fn () {
            ia("tidak bisa bertingkah seperti metode", fn () {
                tidakAda!(fn () { A })

                kelas A {
                    ada b
                    fn b() { b }
                    fn a=(x) { b = x + 5 }

                    ada c = fn (x) { b = x }
                    ada d = fn (x) { ini.b = x }
                    ada e = fn (x) { a = 18 }
                    ada f = fn (x) { ini.a = 18 }

                    fn pengubahC() { c }
                    fn pengubahD() { d }
                    fn pengubahE() { e }
                    fn pengubahF() { f }
                }

                ada a = A.baru()
                tuntut a.b() == nil

                tuntut coba {
                    a.pengubahC()(14)
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "identitas belum terdefinisikan: b"
                    benar
                } == benar

                tuntut a.b() == nil

                tuntut coba {
                    a.pengubahD()(15)
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "identitas belum terdefinisikan: b"
                    benar
                } == benar

                tuntut a.b() == nil

                tuntut coba {
                    a.pengubahE()(15)
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "identitas belum terdefinisikan: a"
                    benar
                } == benar

                tuntut coba {
                    a.pengubahF()(15)
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "identitas belum terdefinisikan: a"
                    benar
                } == benar
            })
        })
    })

    ia("`ini` dapat menyasar fungsi dari instansi tanpa ambiguitas", fn () {
        kelas Penyapa {
            ada nilai

            fn nilai() { nilai }

            fn sapa(nama) {
                ada sapaan = "Yo!"
                ini.nilai = ini.sapaan() + nama
                sapaan
            }

            fn sapaan() {
                "Halo, "
            }
        }

        ada p = Penyapa.baru()
        ada sapaan = p.sapa("Gara")
        tuntut sapaan == "Yo!"
        tuntut p.nilai() == "Halo, Gara"
    })

    ia("`ini` dapat meresolusi pesan berantai", fn () {
        kelas Alamat {
            ada kota
            ada jalan

            fn kota() { kota }
            fn kota(v) { kota = v}

            fn jalan() { jalan }
            fn jalan(v) { jalan = v }
        }

        kelas Sekolah {
            ada nama
            ada alamat

            fn nama() { nama }
            fn nama(v) { nama = v }

            fn alamat() { alamat }
            fn alamat(v) {
                tuntut v itu Alamat
                alamat = v
            }

            fn ubahNamaJalan(namaBaru) {
                ini.alamat().jalan(namaBaru)
            }

            fn alamatLengkap() {
                ini.alamat().jalan() + ", " + alamat().kota()
            }
        }

        ada surabaya = Alamat.baru()
        surabaya.kota("Surabaya")
        surabaya.jalan("Jalan ABC")

        ada jakarta = Alamat.baru()
        jakarta.kota("Jakarta")
        jakarta.jalan("Jalan DEF")

        ada s = Sekolah.baru()
        s.alamat(surabaya)

        tuntut s.alamatLengkap() == "Jalan ABC, Surabaya"
        s.ubahNamaJalan("Jalan Usman Sadar")
        tuntut s.alamatLengkap() == "Jalan Usman Sadar, Surabaya"
        tuntut s.alamat().jalan() == "Jalan Usman Sadar"
    })

    ia("`ini` dapat digunakan berdampingan dengan berbagai macam operator", fn () {
        kelas Koleksi {
            ada nilai = []
            ada checksum = 0

            fn nilai() { nilai }
            fn checksum() { checksum }

            fn masukkan(item) {
                nilai << item
                ini.nilai << item

                checksum += 2
                ini.checksum += 2

                checksum -= 1
                ini.checksum -= 1

                checksum *= 10
                ini.checksum *= 10

                checksum /= 2
                ini.checksum /= 2
            }

            fn jumlah() {
                ada i = 0
                ada jml = 0

                selama (i < nilai.panjang()) {
                    jml += nilai[i]
                    i += 1
                }

                utus jml
            }
        }

        ada k = Koleksi.baru()
        k.masukkan(1)
        k.masukkan(2)

        tuntut k.nilai() == [1, 1, 2, 2]
        tuntut k.jumlah() == 6
        tuntut k.checksum() == 1300
    })

    ia("metode dari suatu kelas, tidak seperti fungsi, tidak dapat diutus keluar", fn () {
        kelas Penyapa {
            ada penyapa = fn () { "Halo!" }
            fn fungsiPenyapa() { penyapa }
            fn katakanSapaan() { penyapa() }

            fn selamatPagi() { "Selamat Pagi" }
            fn fungsiSelamatPagi() { selamatPagi }
            fn katakanSelamatPagi() { selamatPagi() }
        }

        ada p = Penyapa.baru()

        tuntut p.katakanSapaan() == "Halo!"
        tuntut p.katakanSelamatPagi() == "Selamat Pagi"

        tuntut p.fungsiPenyapa() itu Fungsi
        tuntut p.fungsiPenyapa()() == "Halo!"

        //tuntut p.fungsiSelamatPagi() == nil
    })

    ia("mengakses metode yang tidak ada", fn () {
        kelas A {
            fn a(x) { ini.y = x }
            fn b(x) { y = x }
        }

        ada a = A.baru()

        coba { a.z() }
        tahan (e) { tuntut e itu MasalahMetode }

        coba { a.a(10) }
        tahan (e) { tuntut e itu MasalahPengenal }

        coba { a.b(10) }
        tahan (e) { tuntut e itu MasalahPengenal }
    })

    // todo: add test about `ini` on the interpreter level when setReceivingObject is nil `ini.number = `1` directly as a statement

    ia("`ini` dapat merujuk ke variabel instansi atau metode yang sesuai", fn () {
        kelas A {
            ada a = "variabel"
            fn a() { "metode" }

            fn dapatkanVarTanpaIni() { a }
            fn dapatkanVarDenganIni() { ini.a }

            fn dapatkanMetodeTanpaIni() { a() }
            fn dapatkanMetodeDenganIni() { ini.a() }
        }

        ada a = A.baru()
        ada nilaiVar = "variabel"
        ada nilaiMet = "metode"

        tuntut a.a() == nilaiMet
        tuntut a.dapatkanVarTanpaIni() == nilaiVar
        tuntut a.dapatkanVarDenganIni() == nilaiVar

        tuntut a.dapatkanMetodeTanpaIni() == nilaiMet
        tuntut a.dapatkanMetodeDenganIni() == nilaiMet
    })

    ia(".kobit() mencetak kode bibit", fn () {
        tuntut Kelas.kobit() != Masalah.kobit()
        tuntut Masalah.kobit() != MasalahArgumen.kobit()
        tuntut MasalahArgumen.kobit() != MasalahMetode.kobit()
    })

    tentang("blok `pribadi`", fn () {
        ia("bisa mendefinisikan metode yang bersifat pribadi", fn () {
            kelas Penyapa {
                fn selamatPagi(nama) {
                    sapa(nama) + ". Selamat pagi."
                }

                pribadi {
                    fn sapa(nama) {
                        "Halo, " + nama
                    }
                }
            }

            ada p = Penyapa.baru()
            tuntut p.selamatPagi("Adam") == "Halo, Adam. Selamat pagi."

            tuntut coba {
                p.sapa("Adam")
                salah
            } tahan (e) {
                tuntut e itu Masalah
                tuntut e.pesan() == "`sapa` adalah metode pribadi kelas Penyapa yang hanya bisa dipanggil dari dalam"
                benar
            } == benar

            tuntut p.metode().berisi?("sapa") == salah

            // dipanggil dari kelas lain
            kelas A {
                fn b() {
                    B.baru(objekIni()).aAsli()
                }

                pribadi {
                    fn a() { "a" }
                }
            }

            kelas B {
                ada a

                fn sta baru(a) {
                    ada obj = baru()
                    obj.a = a
                    obj
                }

                fn aAsli() {
                    ini.a.a()
                }
            }

            tuntut coba {
                A.baru().b()
                salah
            } tahan (e) {
                cetak(e)
                tuntut e.pesan() == "`a` adalah metode pribadi kelas A yang hanya bisa dipanggil dari dalam"
                benar
            } == benar
        })

        ia("bisa mendefinisikan variabel instansi yang tidak tercetak saat inspeksi", fn () {
            kelas A {
                ada a = 123

                fn a() { a }

                fn b() { b }

                pribadi {
                    ada b = "b"
                }
            }

            ada a = A.baru()
            tuntut a.jadiKata().berisi?("a=123")
            tuntut a.jadiKata().berisi?("b=") == salah
            tuntut a.b() == "b"
        })

        ia("bisa mendefinisikan variabel instansi yang memiliki fungsi yang bisa dipanggil", fn () {
            tidakAda!(fn () { A }) // todo: ubah ke: belumAda! { "A" }

            kelas A {
                fn a() { aPribadi() }

                pribadi {
                    ada aPribadi = fn () { "a" }
                }
            }

            tuntut A.baru().a() == "a"
        })
    })

    tentang("penggunaan kata kunci `sta`", fn () {
        tentang("variabel", fn () {
            ia("tidak bercampur dengan pengenal statis dari kelas-kelas lain", fn () {
                kelas A {
                    ada sta a
                }

                kelas B {
                    ada sta b
                }

                tuntut coba { A.b; salah } tahan (e) { tuntut e.pesan() == "tidak dapat meresolusi identitas statis `b`"; benar } == benar
                tuntut coba { B.a; salah } tahan (e) { tuntut e.pesan() == "tidak dapat meresolusi identitas statis `a`"; benar } == benar
            })

            ia("tidak dapat diubah dari luar kelas", fn () {
                tidakAda!(fn () { A })

                kelas A {
                    ada sta NILAI = 1
                }

                tuntut coba {
                    A.NILAI = 2
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "tidak diperkenankan mengubah nilai variabel statis `NILAI` dari luar kelas"
                    benar
                } == benar

                tuntut A.NILAI == 1
            })

            ia("dapat mengambil nilai dari variabel statis lain", fn () {
                kelas A {
                    ada sta nilaiLama = 123
                    ada sta nilai = nilaiLama
                }

                tuntut A.nilaiLama == 123
                tuntut A.nilai == 123
            })

            ia("hanya dapat mengambil nilai dari pengenal variabel", fn () {
                kelas A {
                    ada sta a = "a1"
                    ada sta staA = a

                    fn a() { "a2" }
                }

                tuntut A.a == "a1"
                tuntut A.staA == "a1"
                tuntut A.baru().a() == "a2"

                tuntut coba {
                    kelas B {
                        ada sta fnB = b
                        fn b () {}
                    }
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "identitas tidak ditemukan: b"
                    benar
                } == benar
            })

            ia("tidak dapat diakses dari suatu instansi", fn () {
                kelas A {
                    fn sta a() {}
                }

                ada a = A.baru()

                coba {
                    a.a()
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "objek tipe A tidak memiliki metode `a` yang bisa dipanggil"
                    benar
                } == benar
            })

            ia("dapat memiliki nilai awal", fn () {
                kelas A {
                    ada sta nilai = "a"
                    ada sta a = "a"
                }

                kelas B {
                    ada sta nilai = "b"
                    ada sta kelasA = A
                    ada sta b = "b"
                }

                // variabel statis dengan pengenal yang berbeda
                tuntut A.a == "a"
                tuntut B.b == "b"

                // variabel statis dengan pengenal yang sama
                tuntut A.nilai == "a"
                tuntut B.nilai == "b"
                tuntut B.kelasA.nilai == "a"
            })

            ia("dapat bersifat statis dan pribadi", fn () {
                kelas A {
                    pribadi {
                        ada sta nilaiPribadi = "ap"
                    }

                    ada sta nilaiDariPribadi1 = nilaiPribadi
                    ada sta nilaiDariPribadi2 = A.nilaiPribadi
                }

                tuntut A.nilaiDariPribadi1 == "ap"
                tuntut A.nilaiDariPribadi2 == "ap"

                tuntut coba {
                    A.nilaiPribadi
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "tidak dapat meresolusi identitas statis `nilaiPribadi` yang bersifat pribadi"
                    benar
                } == benar
            })

            ia("jika tidak dapat diresolusi akan menjadi masalah", fn () {
                kelas A {}

                tuntut coba {
                    A.nilai
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "tidak dapat meresolusi identitas statis `nilai`"
                    benar
                } == benar
            })

            ia("tidak bisa didefinisikan di dalam sebuah metode", fn () {
                tuntut coba {
                    kelas A {
                        fn a() {
                            ada sta x = 12
                        }
                    }
                    A.baru().a()
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "pengenal statis hanya bisa digunakan di luar metode di dalam kelas"
                    benar
                } == benar
            })

            ia("tidak dapat dieksekusi seperti metode kecuali jika fungsi", fn () {
                tidakAda!(fn () { A })

                kelas A {
                    ada sta a = 12
                    ada sta b = fn () { "b" }

                    pribadi {
                        ada sta c = fn () { "c" }
                    }

                    ada sta d = c
                }

                tuntut coba {
                    A.a()
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "kelas A tidak memiliki fungsi statis `a` tak berargumen yang bisa dipanggil"
                    benar
                } == benar

                tuntut A.b itu Fungsi
                tuntut A.b() == "b"

                tuntut coba {
                    A.c()
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "kelas A tidak memiliki fungsi statis `c` tak berargumen yang bisa dipanggil"
                    benar
                } == benar

                tuntut A.d() == "c"
            })
        })

        tentang("fungsi", fn () {
            ia("bermasalah jika fungsi statis tidak terdefinisikan", fn () {
                tidakAda!(fn () { A })

                kelas A {}

                tuntut coba {
                    A.XYZ = 12
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "kelas A tidak memiliki fungsi statis `XYZ=` berargumen 1 yang bisa dipanggil"
                    benar
                } == benar

                tuntut coba {
                    A.xyz()
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "kelas A tidak memiliki fungsi statis `xyz` tak berargumen yang bisa dipanggil"
                    benar
                } == benar
            })

            ia("bisa mendefinisikan fungsi statis", fn () {
                tidakAda!(fn () { A })

                kelas A {
                    fn sta a() { "a" }
                }

                tuntut A.a() == "a"
            })

            ia("bisa bernama sama dengan metode", fn () {
                tidakAda!(fn () { A })

                kelas A {
                    fn sta a() { "statis" }
                    fn a() { "metode" }
                }

                ada a = A.baru()
                tuntut a.a() == "metode"
                tuntut A.a() == "statis"
            })

            ia("fungsi statis bisa memiliki argumen yang bernama sama dengan identitas fungsi", fn () {
                tidakAda!(fn () { A })

                kelas A {
                    fn sta x(x) { x.jadiKata() }
                }

                tuntut A.x(1) == "1"
                tuntut A.x([1,2,3]) == [1,2,3].jadiKata()
            })

            ia("bisa memanggil fungsi statis", fn () {
                tidakAda!(fn () { A })

                kelas A {
                    fn sta a() { "a" }
                    fn sta b() { "b" }
                    fn sta ab() { a() + b() }
                    fn sta abc() { a() + b() + c() }

                    fn sta x(x) { x.jadiKata() }
                    fn sta abcx(arg) { abc() + x(arg) }

                    pribadi {
                        fn sta c() { "c" }
                    }
                }

                tuntut A.a() == "a"
                tuntut A.b() == "b"
                tuntut A.ab() == "ab"
                tuntut A.abc() == "abc"
                tuntut A.x(123) == "123"
                tuntut A.abcx(123) == "abc123"
            })

            ia("bisa diresolusi dengan atau tanpa menggunakan identitas kelas", fn () {
                tidakAda!(fn () { A })

                kelas A {
                    fn sta x(x) { x.jadiKata() }
                    fn sta aa() { x("a") + A.x("a") }
                }

                tuntut A.aa() == "aa"
            })

            ia("bisa diresolusi meski multi-bentuk", fn () {
                tidakAda!(fn () { A })

                kelas A {
                    fn sta a(arg1, arg2) { arg1 + arg2 }
                    fn sta a(arg1) { A.a(arg1, 1) + a(arg1, 1) }
                    fn sta a() { a(1) }
                }

                tuntut A.a(1, 1) == 2
                tuntut A.a(1) == 4
                tuntut A.a() == 4
            })

            ia("tidak dianggap metode kelas", fn () {
                tidakAda!(fn () { A })

                kelas A {
                    fn sta a() { "a" }
                }

                ada a = A.baru()
                tuntut !a.metode().berisi?("a")

                tuntut coba {
                    a.a()
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "objek tipe A tidak memiliki metode `a` yang bisa dipanggil"
                    benar
                } == benar
            })

            ia("bisa bersifat pribadi", fn () {
                kelas A {
                    fn sta a() { "a" }
                    fn sta ab() { a() + b() }

                    pribadi {
                        fn sta b() { "b" }
                    }
                }

                tuntut A.a() == "a"
                tuntut A.ab() == "ab"

                tuntut coba {
                    A.b()
                    salah
                } tahan (e) {
                    tuntut e.pesan() == "`b` adalah fungsi statis bersifat pribadi kelas A yang hanya bisa dipanggil dari dalam"
                    benar
                } == benar
            })

            tentang("fungsi statis `#baru()`", fn () {
                ia("harus mengutus objek dengan kelas yang sama", fn () {
                    tidakAda!(fn () { A })

                    kelas A {
                        fn sta baru() { 1 } // mengutus yang bukan instansi A
                        fn sta baru(arg) { arg } // mengutus yang bukan instansi A
                        fn sta baru(arg1, arg2) { Refleksi.cipta!(A) } // menciptakan kelas lewat refleksi
                        fn sta baru(arg1, arg2, arg3) { baru() } // memanggil baru tanpa identitas kelas
                    }

                    tuntut coba {
                        A.baru()
                        salah
                    } tahan (e) {
                        tuntut e itu MasalahKelas
                        tuntut e.pesan() == "proses inisialisasi harus mengutus objek bertipe kelas yang sama"
                        benar
                    } == benar

                    tuntut coba {
                        A.baru()
                        salah
                    } tahan (e) {
                        tuntut e itu MasalahKelas
                        tuntut e.pesan() == "proses inisialisasi harus mengutus objek bertipe kelas yang sama"
                        benar
                    } == benar

                    tuntut coba {
                        A.baru(1)
                        salah
                    } tahan (e) {
                        tuntut e itu MasalahKelas
                        tuntut e.pesan() == "proses inisialisasi harus mengutus objek bertipe kelas yang sama"
                        benar
                    } == benar

                    tuntut A.baru(1, 2) itu A

                    tuntut coba {
                        A.baru(1, 2, 3)
                        salah
                    } tahan (e) {
                        tuntut e.pesan() == "proses inisialisasi harus mengutus objek bertipe kelas yang sama"
                        benar
                    } == benar
                })

                ia("bisa menjadi fungsi statis pribadi", fn () {
                    tidakAda!(fn () { A })

                    kelas A {
                        ada a

                        fn a() { a }
                        fn a=(x) { a = x }

                        fn sta baru(a) {
                            ada obj = baru()
                            obj.a = a
                            obj
                        }

                        pribadi {
                            fn sta baru() { super() }
                        }
                    }

                    ada a = A.baru(123)
                    tuntut a.a() == 123

                    tuntut coba {
                        A.baru()
                        salah
                    } tahan (e) {
                        tuntut e.pesan() == "`baru` adalah fungsi statis bersifat pribadi kelas A yang hanya bisa dipanggil dari dalam"
                        benar
                    } == benar
                })

                ia("dapat mendefinisikan fungsi statis `.baru()` dengan varian argumen lain", fn () {
                    tidakAda!(fn () { A })

                    kelas A {
                        ada a
                        fn a() { a }
                        fn a=(x) { a = x }

                        fn sta baru(arg) {
                            ada obj = baru()
                            obj.a = "a" + arg
                            obj
                        }
                    }

                    ada a = A.baru("a")
                    tuntut a.a() == "aa"
                })

                ia("dapat menjadikan pribadi fungsi `baru` yang tanpa argumen", fn () {
                    tidakAda!(fn () { A })

                    kelas A {
                        ada a
                        fn a() { a }
                        fn a=(x) { a = x }

                        fn sta baru(a) {
                            ada obj = baru()
                            obj.a = a
                            obj
                        }

                        pribadi {
                            fn sta baru() { super() }
                        }
                    }

                    tuntut A.baru(nil).a() == nil
                    tuntut A.baru(123).a() == 123
                    tuntut A.baru("123").a() == "123"
                })
            })
        })
    })

    tentang("kelas dalam namespace", fn () {
        ia("bisa didefinisikan dan diresolusi menggunakan `::`", fn () {
            tidakAda!(fn () { A })
            tidakAda!(fn () { A::B })
            tidakAda!(fn () { A::C })

            kelas A {
                kelas B {}
                kelas C {
                    kelas D {
                        kelas E {}
                    }
                }
            }

            tuntut A itu Kelas
            tuntut A::B itu Kelas
            tuntut A::C itu Kelas
            tuntut A::C::D itu Kelas
            tuntut A::C::D::E itu Kelas

            tuntut A::B.baru() itu A::B
            tuntut A::C::D::E.baru() itu A::B == salah

            tidakAda!(fn () { B })
            tidakAda!(fn () { C })
            tidakAda!(fn () { B::C })
            tidakAda!(fn () { A::B::C })
        })

        ia("menyatakan nama kelas fully-qualified tanpa `::` jika tidak ditemukan", fn () {
            tidakAda!(fn () { A })

            tuntut coba {
                ::A
                salah
            } tahan (e) {
                tuntut e.pesan() == "identitas tidak ditemukan: A"
                benar
            } == benar

            kelas A {}

            tuntut A itu Kelas
            tuntut ::A itu Kelas
            tuntut ::A == A
            tuntut ::A == ::A
            tuntut A == A
        })

        ia("bisa menjangkau kelas dengan nama yang sama yang berada di luar", fn () {
            kelas A {}

            kelas B {
                kelas A {}

                fn sta aKu() { A.baru() } // tes menerima pemanggilan
                fn sta aKu?(a) { a.tipe() == A } // tes pengenal

                fn sta aNya() { ::A.baru() } // tes menerima pemanggilan
                fn sta aNya?(a) { a.tipe() == ::A } // tes pengenal
            }

            tuntut B.aKu?(B::A.baru()) == benar
            tuntut B.aKu?(A.baru()) == salah

            tuntut B.aNya?(B::A.baru()) == salah
            tuntut B.aNya?(A.baru()) == benar

            tuntut B.aKu() itu B::A
            tuntut B.aNya() itu A
        })

        ia("pengenal kelas tidak boleh diawali dengan `::`", fn () {
            tuntut coba {
                kelas ::A {}
                salah
            } tahan (e) {
                tuntut e.pesan() == "pengenal kelas tidak boleh diawali dengan `::`"
                benar
            } == benar

            tuntut coba {
                kelas A {
                    kelas ::B {}
                }
                salah
            } tahan (e) {
                tuntut e.pesan() == "pengenal kelas tidak boleh diawali dengan `::`"
                benar
            } == benar
        })

        ia("bisa diresolusi dengan benar meski ada kelas dengan nama yang mirip tapi dalam namespace yang berbeda", fn () {
            tidakAda!(fn () { Koneksi })
            tidakAda!(fn () { Sertifikat })
            tidakAda!(fn () { HTTP::Koneksi })

            kelas Koneksi {}
            kelas Sertifikat {}

            kelas HTTP {
                kelas Koneksi {}

                fn sta buatKoneksi() {
                    Koneksi.baru() // tes menerima panggilan
                }

                fn sta sekoneksi?(a) {
                    a.tipe() == Koneksi // tes pengenal
                }

                fn sta periksaSertifikat(s) {
                    s.tipe() == Sertifikat // test pengenal
                }

                fn sta buatSertifikat() {
                    Sertifikat.baru() // tes menerima panggilan
                }
            }

            tuntut Koneksi.baru() itu Koneksi
            tuntut HTTP::Koneksi.baru() itu HTTP::Koneksi
            tuntut HTTP.buatKoneksi() itu HTTP::Koneksi
            tuntut HTTP.sekoneksi?(Koneksi.baru()) == salah
            tuntut HTTP.sekoneksi?(HTTP::Koneksi.baru()) == benar
            tuntut HTTP.buatSertifikat() itu Sertifikat
            tuntut HTTP.periksaSertifikat(Sertifikat.baru()) == benar
            tuntut HTTP.periksaSertifikat(HTTP::Koneksi.baru()) == salah

            tidakAda!(fn () { A })

            kelas A {
                kelas B {}
            }

            tuntut A itu Kelas
            tuntut A::B itu Kelas
        })

        ia("tidak bisa didefinisikan jika hirarki kelas sebelumnya belum terdefinisikan", fn () {
            tuntut coba {
                kelas A::B {}
                salah
            } tahan (e) {
                tuntut e.pesan() == "tidak bisa mendefinisikan kelas karena `A` belum terdefinisikan"
                benar
            } == benar

            tuntut coba {
                kelas A::B::C {}
                salah
            } tahan (e) {
                tuntut e.pesan() == "tidak bisa mendefinisikan kelas karena `A::B` belum terdefinisikan"
                benar
            } == benar

            tuntut coba {
                kelas A {
                    kelas B {}
                    kelas B::C {}
                    kelas B::C::D::E {}
                }
                salah
            } tahan (e) {
                tuntut e.pesan() == "tidak bisa mendefinisikan kelas karena `A::B::C::D` belum terdefinisikan"
                benar
            } == benar
        })

        ia("jika kelas utama bersifat sementara, maka kelas dalam kelas juga bersifat sementara", fn () {
            ada cek = []

            fn () {
                kelas A {
                    kelas B {}
                    kelas C {}
                }

                cek << A itu Kelas
                cek << A::B itu Kelas
                cek << A::C itu Kelas
            }()

            tuntut cek == [benar, benar, benar]

            tidakAda!(fn() { A })
            tidakAda!(fn () { A::B })
            tidakAda!(fn () { A::C })
        })
    })

    // todo: pindah ke sini
    tentang("tingkah laku kelas", fn () {

    })

    // uji coba tentang metode dari kelas Kelas
    tentang("metode Kelas", fn () {
        tentang(".jadiKata", fn () {
            ia("bisa mencetak instansi variabel yang mereferensikan diri sendiri", fn () {
                kelas A {
                    ada a

                    fn a() { a = objekIni() }
                }

                ada a = A.baru()
                a.a()
                tuntut a.jadiKata().berisi?("a=<ini>")
            })
        })
    })
})
